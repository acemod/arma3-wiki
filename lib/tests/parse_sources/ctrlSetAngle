{{RV|type=command

|game1= arma3
|version1= 1.62

|arg= local

|eff= local

|gr1= GUI Control

|descr= Sets the rotation of a control set as {{hl|ST_PICTURE}}. Rotation will be done within the control boundaries, so if the X and Y are given off center, the picture may get clipped by the control boundaries. Only requires [[ctrlCommit]] when fourth parameter is set to [[false]]. Procedural textures filled controls do not rotate. Supported control types:
* [[CT_STATIC]]
* [[CT_ACTIVETEXT]] (Since Arma 3 v2.09.149642)

{{Feature|important|Images that touch borders may "bleed" during rotation transformation, to avoid this, make sure the image has 1px transparent padding}} 

|s1= control [[ctrlSetAngle]] [angle, centerX, centerY, now]

|p1= control: [[Control]] - picture control

|p2= angle: [[Number]] - Rotation angle (clockwise)

|p3= centerX: [[Number]] - Rotation center X in range 0 to 1. Control center is 0.5

|p4= centerY: [[Number]] - Rotation center Y in range 0 to 1. Control center is 0.5

|p5= now: [[Boolean]] - (Optional, default [[true]]) If [[false]], control will wait for [[ctrlCommit]] input for result to apply
|p5since= arma3 1.78

|r1= [[Nothing]]

|x1= <sqf>_control ctrlSetAngle [25, 0.5, 0.5];</sqf>

|x2= <sqf>with uiNamespace do
{
	ctrl = findDisplay 46 ctrlCreate ["RscPictureKeepAspect", -1];
	ctrl ctrlSetPosition [0,0,1,1];
	ctrl ctrlSetText "A3\Missions_F_Exp\data\Img\lobby\ui_campaign_lobby_background_tablet_radial_left_ca.paa";
	ctrl ctrlCommit 0;
	angle = 0;
	onEachFrame
	{
		with uiNamespace do
		{
			if (angle > 359) then {angle = 0};
			ctrl ctrlSetAngle [angle, 0.5, 0.5];
			angle = angle + 1;
		};
	};
};</sqf>

|x3= <sqf>_control ctrlSetAngle [25, 0.5, 0.5, false];
_control ctrlCommit 1; // Rotation applies smoothly for 1 second.</sqf>

|seealso= [[ctrlAngle]]
}}